// Copy build and script dependencies from from apps/zxtune-android/zxtune

buildscript {
    repositories {
        jcenter()
        google()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0'
        classpath 'io.fabric.tools:gradle:1.31.2'
    }
}

plugins {
    id 'java-library'
}

repositories {
  jcenter()
  google()
}

task resolveDependencies {
  setDescription "Resolves all projects dependencies from the repository."
  setGroup "Build Server"

  doLast {
    rootProject.allprojects { project ->
      project.buildscript.configurations.forEach { configuration ->
        if (configuration.canBeResolved) {
          configuration.resolve()
        }
      }

      project.configurations.forEach { configuration ->
        if (configuration.canBeResolved) {
          configuration.resolve()
        }
      }
    }
  }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.appcompat:appcompat-resources:1.1.0'
    implementation 'androidx.media:media:1.0.0'
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0-rc01'
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }
    implementation('com.crashlytics.sdk.android:crashlytics-ndk:2.1.1@aar') {
        transitive = true
    }
    implementation('com.crashlytics.sdk.android:answers:1.4.7@aar') {
        transitive = true
    }
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'com.github.anrwatchdog:anrwatchdog:1.4.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation "androidx.room:room-runtime:2.2.5"
    annotationProcessor "androidx.room:room-compiler:2.2.5"

    //manual deps
    implementation 'androidx.databinding:databinding-runtime:3.2.0'
    implementation 'androidx.databinding:databinding-compiler:3.2.0'
    implementation 'androidx.databinding:databinding-adapters:3.2.0'
}
